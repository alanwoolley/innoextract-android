buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.8.10"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply from: 'versioning.gradle'

project.ext.version = "2.0.0"
version project.ext.version
group "uk.co.armedpineapple.innoextract"

android {
    compileSdkVersion 34

    namespace 'uk.co.armedpineapple.innoextract.service'

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }

        aarMetadata {
            minCompileSdk = 26
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'joda-time:joda-time:2.12.5'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation "com.lazygeniouz:dfc:1.0.7"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'uk.co.armedpineapple.innoextract'
            artifactId 'service'
            version project.ext.version
            artifact("$buildDir/outputs/aar/service-release.aar")
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/alanwoolley/innoextract-android")
            credentials {
                username = System.getenv("GPR_USER")
                password = System.getenv("GPR_API_KEY")
            }
        }
    }
}

repositories {
    mavenCentral()
}


