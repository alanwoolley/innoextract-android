apply plugin: 'maven-publish'
apply plugin: 'signing'

// If you want to publish your sources as well
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = GROUP_ID
version = VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION

                from components.release
//                artifact androidSourcesJar

                pom {
                    name = ARTIFACT_ID
                    description = 'An Android library for extracting Inno Setup installers.'
                    url = SITE_URL
                    licenses {
                        // Your licensing information
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://raw.githubusercontent.com/alanwoolley/innoextract-android/develop/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'alanwoolley'
                            name = 'Alan Woolley'
                            email = 'alan@armedpineapple.co.uk'
                        }
                    }

                    scm {
                        connection = GIT_URL
                        developerConnection = GIT_URL
                        url = SITE_URL
                    }
                }
            }
        }
    }

    generateMetadataFileForReleasePublication.dependsOn androidSourcesJar
}

signing {
    useInMemoryPgpKeys(
            System.getenv("OSS_SIGNING_KEY_ID"),
            System.getenv("OSS_SIGNING_KEY"),
            System.getenv("OSS_SIGNING_PASSWORD"),
    )
    sign publishing.publications
}